module Compiler.MIO.To.Interm

open FStar.Tactics
open FStar.Tactics.Typeclasses
open FStar.Ghost

open Compiler.Languages
open TC.Checkable


(** **** Types **)
(** Dynamic check possibilities:
pre:
  trace -> bool
  'a -> trace -> bool

post:
  trace -> trace -> bool
  'a -> trace -> trace -> bool
  trace -> 'b -> trace -> bool
  'a -> trace -> 'b -> trace -> bool

All these dynamic checks can be encoded using:
  'a -> trace -> 'b -> trace -> bool
and do some hacks in the code.

One extra thing would be the dynamic checks to be also
used to do the refinements. Right now, a different checkable
typeclass is used for the refinements.
**)

(** **** Tree **)
type tree (a: Type) =
  | Leaf : tree a
  | EmptyNode: left: tree a -> right: tree a -> tree a
  | Node: data: a -> left: tree a -> right: tree a -> tree a

let root (t:(tree 'a){Node? t}) = Node?.data t
(** TODO: refactor these into two utils **)
let left (t:(tree 'a){Node? t \/ EmptyNode? t}) : tree 'a = 
  match t with 
  | Node _ lt _ -> lt
  | EmptyNode lt _ -> lt

let right (t:(tree 'a){Node? t \/ EmptyNode? t}) : tree 'a = 
  match t with 
  | Node _ _ rt -> rt
  | EmptyNode _ rt -> rt
  
let rec equal_trees (t1:tree 'a) (t2:tree 'a) =
  match t1, t2 with
  | Leaf, Leaf -> True
  | EmptyNode lhs1 rhs1, EmptyNode lhs2 rhs2 -> equal_trees lhs1 lhs2 /\ equal_trees rhs1 rhs2
  | Node x lhs1 rhs1, Node y lhs2 rhs2 -> x == y /\ equal_trees lhs1 lhs2 /\ equal_trees rhs1 rhs2
  | _, _ -> False

(* The function above is really just propositional equality. See this proof. *)
let rec equal_trees_prop (t1 t2 : tree 'a) : Lemma (equal_trees t1 t2 <==> t1 == t2)
  [SMTPat (equal_trees t1 t2)]
  =
  match t1, t2 with
  | Leaf,            Leaf -> ()
  | EmptyNode l1 r1, EmptyNode l2 r2 
  | Node _ l1 r1,    Node _ l2 r2 -> equal_trees_prop l1 l2; equal_trees_prop r1 r2
  | _ -> ()

let rec map_tree (t:tree 'a) (f:'a -> 'b) : tree 'b =
  match t with
  | Leaf -> Leaf
  | EmptyNode lhs rhs -> EmptyNode (map_tree lhs f) (map_tree rhs f)
  | Node x lhs rhs -> Node (f x) (map_tree lhs f) (map_tree rhs f)
  
let rec map_fuse (t:tree 'a) (f:'a -> 'b) (g : 'b -> 'c)
  : Lemma (map_tree (map_tree t f) g == map_tree t (fun x -> g (f x)))
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_fuse lhs f g; map_fuse rhs f g
  | Node x lhs rhs -> map_fuse lhs f g; map_fuse rhs f g
  
let rec map_id (t:tree 'a)
  : Lemma (map_tree t (fun x -> x) == t)
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_id lhs; map_id rhs
  | Node x lhs rhs -> map_id lhs; map_id rhs
  
let rec map_ext (t:tree 'a) (f:'a -> 'b) (g : 'a -> 'b)
  : Lemma (requires (forall x. f x == g x))
          (ensures (map_tree t f == map_tree t g))
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_ext lhs f g; map_ext rhs f g
  | Node x lhs rhs -> map_ext lhs f g; map_ext rhs f g

(* From h', an extension of h, we can obtain the difference
(i.e. the local trace) just from the lengths. *)
let get_local_trace (h':trace) (h:trace) :
  Pure trace
    (requires (h' `suffix_of` h))
    (ensures (fun lt -> h == (apply_changes h' lt))) =
  suffix_of_length h' h;
  let n : nat = (List.length h) - (List.length h') in
  let (lt', ht) = List.Tot.Base.splitAt n h in
  lemma_splitAt_equal n h;
  lemma_splitAt_suffix h h';
  List.Tot.Properties.rev_involutive lt';
  assert (h == apply_changes h' (List.rev lt'));
  List.rev lt'

(* Runtime check typ *)
type dc_typ (argt:Type u#a) (rett:Type u#b) = argt -> trace -> rett -> trace -> bool

(* Postcondition for effectful runtime check *)
let eff_dc_typ_cont_post (dc:dc_typ 'a 'b) (initial_h:erased trace) (x:'a) (y:'b) (current_h:trace) (b:bool) (lt:trace) : Type0 =
  (initial_h `suffix_of` current_h) /\
  (let the_lt = get_local_trace initial_h current_h in
    apply_changes initial_h the_lt == current_h // the_lt = current_h - initial_h (implied by the spec of get_local_trace...)
    /\ lt == [] // no events generated by this check
    /\ (b <==> dc x initial_h y the_lt)) // the result is the same as the pure dc.

(* Effectful runtime check for the result, given initial value
and initial history *)
type eff_dc_typ_cont (fl:erased tflag) (t1:Type u#a) (t2:Type u#b) (dc:dc_typ t1 t2) (x:t1) (initial_h:erased trace) =
  y:t2 -> MIO bool fl (fun h -> initial_h `suffix_of` h) (eff_dc_typ_cont_post dc initial_h x y)
  
(* Effectful runtime check: given an x:t1 returns an erased trace and an
effectful function to check the result (t2) *)
type eff_dc_typ (fl:erased tflag) (#t1 #t2:Type) (dc:dc_typ t1 t2) =
  x:t1 -> MIO (initial_h:(erased trace) & eff_dc_typ_cont fl t1 t2 dc x initial_h)
             fl
             (fun _ -> True)
             (fun h (| initial_h, _ |) lt -> h == reveal initial_h /\ lt == [])

(* Lifting a runtime check into an effectful check *)
val enforce_dc : (#argt:Type u#a) -> (#rett:Type u#b) -> dc:dc_typ argt rett -> eff_dc_typ AllActions dc
let enforce_dc #argt #rett dc x =
  let initial_h = get_trace () in
  let cont : eff_dc_typ_cont AllActions argt rett dc x (hide initial_h) =
    (fun y -> (
      let current_h = get_trace () in
      let lt = get_local_trace initial_h current_h in
      dc x initial_h y lt)) in
  (| hide initial_h, cont |)

// todo: in HO cases, t1 or t2 should be unit since one can not write a
// runtime check that uses an MIO arrow. thus, one idea is to make the
// type of pck_dc here `pck_dc_typ (option t1) (option t2)`
type pck_dc = (argt:Type u#a & rett:Type u#b & dc_typ argt rett)

let arg_typ (ctr:pck_dc) : Type = Mkdtuple3?._1 ctr
let ret_typ (ctr:pck_dc) : Type = Mkdtuple3?._2 ctr

let check (ctr:pck_dc) (arg:arg_typ ctr) (h:trace) (ret:ret_typ ctr) (lt:trace) : bool =
  Mkdtuple3?._3 ctr arg h ret lt

type eff_pck_dc (fl:erased tflag) = ctr:pck_dc & eff_dc_typ fl (Mkdtuple3?._3 ctr)

val make_dc_eff : pck_dc u#a u#b -> eff_pck_dc u#a u#b AllActions
let make_dc_eff r = (| r, (enforce_dc (Mkdtuple3?._3 r)) |)

type typ_eff_dcs (fl:erased tflag) (dcs:tree pck_dc) =
  eff_dcs:(tree (eff_pck_dc fl)){equal_trees dcs (map_tree eff_dcs dfst)}

(* Two helpers, to guide F* *)
let typ_left (#fl:erased tflag) (#dcs:tree pck_dc)
  ($t : typ_eff_dcs fl dcs{Node? t \/ EmptyNode? t})
  : typ_eff_dcs fl (left dcs)
  = left t

let typ_right (#fl:erased tflag) (#dcs:tree pck_dc)
  ($t : typ_eff_dcs fl dcs{Node? t \/ EmptyNode? t})
  : typ_eff_dcs fl (right dcs)
  = right t
  
let cong (#t1 #t2 : tree 'a) (f : 'a -> 'b) (_ : squash (t1 == t2)) : squash (map_tree t1 f == map_tree t2 f) = ()

let make_dcs_eff (dcs:tree pck_dc) : typ_eff_dcs AllActions dcs =
  let r : tree (eff_pck_dc AllActions) = map_tree dcs make_dc_eff in
  let r_def : squash (r == map_tree dcs make_dc_eff) = () in
  let comp x = dfst (make_dc_eff x) in
  (* sigh, F* not being very helpful here *)
  calc (==) {
    map_tree r dfst;
    == { cong dfst r_def }
    map_tree (map_tree #pck_dc dcs make_dc_eff) dfst;
    == { map_fuse #_ #(eff_pck_dc AllActions) #_ dcs make_dc_eff dfst }
    map_tree dcs comp;
    == { map_ext dcs (fun x -> dfst (make_dc_eff x)) (fun x -> x) }
    map_tree dcs (fun x -> x);
    == { map_id dcs }
    dcs;
  };
  r

class exportable (styp : Type u#a) (fl:erased tflag) (pi:policy_spec) (dcs:tree (pck_dc u#c u#d)) = {
  [@@@no_method]
  ityp : Type u#b;
  [@@@no_method]
  c_ityp : interm ityp fl pi;
  [@@@no_method]
  export : typ_eff_dcs fl dcs -> styp -> ityp;
}

class safe_importable (styp : Type u#a) (fl:erased tflag) (pi:policy_spec) (dcs:tree (pck_dc u#c u#d)) = {
  [@@@no_method]
  sityp : Type u#b;
  [@@@no_method]
  c_sityp : interm sityp fl pi;
  [@@@no_method]
  safe_import : sityp -> typ_eff_dcs fl dcs -> styp; 
}

class importable (styp : Type u#a) (fl:erased tflag) (pi:policy_spec) (dcs:tree (pck_dc u#c u#d)) = {
  [@@@no_method]
  ityp : Type u#b; 
  [@@@no_method]
  c_ityp : interm ityp fl pi;
  [@@@no_method]
  import : ityp -> typ_eff_dcs fl dcs -> resexn styp;
}

instance interm_importable_super styp fl pi dcs (d : importable styp fl pi dcs) : interm d.ityp fl pi = d.c_ityp
instance interm_exportable_super styp fl pi dcs (d : exportable styp fl pi dcs) : interm d.ityp fl pi = d.c_ityp
instance interm_safe_importable_super styp fl pi dcs (d : safe_importable styp fl pi dcs) : interm d.sityp fl pi = d.c_sityp

(** *** Exportable instances **)

instance interm_is_exportable (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){Leaf? dcs}) t {| d1: interm t fl pi |} : exportable t fl pi dcs = {
  ityp = t;
  c_ityp = solve;
  export = (fun Leaf x -> x)
}

instance exportable_unit (#fl:erased tflag) (#pi:policy_spec) : exportable unit fl pi Leaf = {
  ityp = unit;
  c_ityp = solve;
  export = (fun Leaf () -> ())
}

instance exportable_file_descr (#fl:erased tflag) (#pi:policy_spec) : exportable file_descr fl pi Leaf = {
  ityp = file_descr;
  c_ityp = solve;
  export = (fun Leaf fd -> fd)
}

instance exportable_bytes (#fl:erased tflag) (#pi:policy_spec) : exportable Bytes.bytes fl pi Leaf = {
  ityp = Bytes.bytes;
  c_ityp = solve;
  export = (fun Leaf b -> b)
}

instance exportable_refinement (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc) t {| d:exportable t fl pi dcs |} (p : t -> Type0) : exportable (x:t{p x}) fl pi dcs = {
  ityp = d.ityp;
  c_ityp = solve;
  export = d.export
}

instance exportable_option
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc)
  t1 {| d1:exportable t1 fl pi dcs |} :
  Tot (exportable (option t1) fl pi dcs) = {
  ityp = option d1.ityp;
  c_ityp = solve;
  export = (fun eff_dcs x -> match x with | Some x' -> Some (d1.export eff_dcs x') | None -> None)
}


instance exportable_pair
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 {| d1:exportable t1 fl pi (left dcs) |} t2 {| d2:exportable t2 fl pi (right dcs) |} :
  Tot (exportable (t1 * t2) fl pi dcs) = {
  ityp = d1.ityp * d2.ityp;
  c_ityp = solve;
  export = (fun eff_dcs (x, y) -> (d1.export (left eff_dcs) x, d2.export (right eff_dcs) y));
}

instance exportable_either
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 {| d1:exportable t1 fl pi (left dcs) |} t2 {| d2:exportable t2 fl pi (right dcs) |} :
  Tot (exportable (either t1 t2) fl pi dcs) = {
  ityp = either d1.ityp d2.ityp;
  c_ityp = solve;
  export = (fun eff_dcs x -> 
      match x with | Inl x -> Inl (d1.export (left eff_dcs) x) | Inr x -> Inr (d2.export (right eff_dcs) x))
}

(** *** Exportable arrows **)

instance exportable_arrow_with_no_pre_and_no_post
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  (t1:Type) {| d1:importable t1 fl pi (left dcs) |}
  (t2:Type) {| d2:exportable t2 fl pi (right dcs) |} :
  exportable (t1 -> MIOpi (resexn t2) fl pi) fl pi dcs = {
    ityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
    c_ityp = solve;
    export = (fun eff_dcs (f:(t1 -> MIOpi (resexn t2) fl pi)) (x:d1.ityp) ->
      match d1.import x (left eff_dcs) with
      | Inl x' -> begin
        match f x' with 
        | Inl x'' -> Inl (d2.export (right eff_dcs) x'') 
        | Inr err -> Inr err
      end
      | Inr err -> Inr err
    )
  }

(** This is a design choice for making proofs easier. One can remove the post-condition **)
instance exportable_arrow_post_args
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 {| d1:importable t1 fl pi (left dcs) |}
  t2 {| d2:exportable t2 fl pi (right dcs) |}
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c1 : squash (forall x h lt r. post x h r lt ==> enforced_locally pi h lt)) :
  exportable (x:t1 -> MIO (resexn t2) fl (fun _ -> True) (post x)) fl pi dcs = {
    ityp = x:d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
    c_ityp = solve;
    export = (fun eff_dcs (f:(x:t1 -> MIO (resexn t2) fl (fun _ -> True) (post x))) ->
      let f' : t1 -> MIOpi (resexn t2) fl pi = f in
      (exportable_arrow_with_no_pre_and_no_post t1 #d1 t2 #d2).export eff_dcs f');
  }

instance exportable_arrow_post
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 {| d1:importable t1 fl pi (left dcs) |}
  t2 {| d2:exportable t2 fl pi (right dcs) |}
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c1 : squash (forall h lt r. post h r lt ==> enforced_locally pi h lt)) :
  exportable (t1 -> MIO (resexn t2) fl (fun _ -> True) post) fl pi dcs = 
  exportable_arrow_post_args t1 t2 (fun _ -> post)

let trivialize_new_post #a #b (pre: a -> trace -> bool) post :
  Tot (a -> trace -> resexn b -> trace -> Type0) =
    fun x h r lt -> 
      (~(pre x h) ==> r == (Inr Contract_failure) /\ lt == []) /\
      (pre x h ==> post x h r lt) 

let enforce_pre
  #t1 #t2
  (#fl:erased tflag)
  (pre : trace -> Type0)
  (dc : dc_typ unit unit)
  (eff_dc : eff_dc_typ fl dc) 
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h. dc () h () [] ==> pre h))
  (f:(t1 -> MIO (resexn t2) fl pre post))
  (x:t1) :
  MIO (resexn t2) fl (fun _ -> True) (trivialize_new_post (fun x h -> dc () h () []) (fun _ -> post) ()) =
  let (| h, eff_dc' |) : (initial_h:(erased trace) & eff_dc_typ_cont fl unit unit dc () initial_h) = eff_dc () in
  if eff_dc' () then f x 
  else Inr Contract_failure

let enforce_pre_args
  #t1 #t2
  (#fl:erased tflag)
  (pre : t1 -> trace -> Type0)
  (dc : dc_typ t1 unit)
  (eff_dc : eff_dc_typ fl dc) 
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h x. dc x h () [] ==> pre x h))
  (f:(x:t1 -> MIO (resexn t2) fl (pre x) (post x)))
  (x:t1) :
  MIO (resexn t2) fl (fun _ -> True) (trivialize_new_post (fun x h -> dc x h () []) post x) =
  let (| h, eff_dc' |) : (initial_h:(erased trace) & eff_dc_typ_cont fl t1 unit dc x initial_h) = eff_dc x in
  if eff_dc' () then f x 
  else Inr Contract_failure

let rityp_dc (#a #b #c #d:Type) (dc:dc_typ a b) : Pure (dc_typ c d) (requires (a == c /\ b == d)) (ensures (fun _ -> True)) = dc

val rityp_eff_dc : (#fl:erased tflag) -> (#a:Type u#a) -> (#b:Type u#b) -> (#c:Type{c == a}) -> (#d:Type{d == b}) -> (#dc:dc_typ a b) -> (t : eff_dc_typ fl dc) -> (eff_dc_typ fl #c #d (rityp_dc dc))
let rityp_eff_dc #fl #a #b #c #d #dc eff_dc (x:c) = 
    let (| initial_h, cont |) = eff_dc x in
    let cont' : eff_dc_typ_cont fl c d (rityp_dc dc) x initial_h = (fun (y:d) -> cont y) in
    (| initial_h, cont' |)

instance exportable_arrow_pre_post_args
  (t1:Type) (t2:Type)
  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ arg_typ (root dcs) == t1 /\ (ret_typ (root dcs) == unit)})
  {| d1:importable t1 fl pi (left dcs) |}
  {| d2:exportable t2 fl pi (right dcs) |}
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h x. check (root dcs) x h () [] ==> pre x h))
  (#c1 : squash (forall x h lt r. pre x h /\ post x h r lt ==> enforced_locally pi h lt)) :
  exportable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) fl pi dcs = {
    ityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi; 
    c_ityp = solve;
    export = (fun eff_dcs (f:(x:t1 -> MIO (resexn t2) fl (pre x) (post x))) ->
      let (| (| a, b, dc |), eff_dc |) = root eff_dcs in
      let eff_dc : eff_dc_typ fl #t1 #unit dc = rityp_eff_dc eff_dc in
      let f' = enforce_pre_args pre dc eff_dc post f in
      let dc_pre = (fun x h -> dc x h () []) in
      let new_post = trivialize_new_post dc_pre post in
      let dcs' = (EmptyNode (left dcs) (right dcs)) in
      let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
      let d = (exportable_arrow_post_args #fl #pi #dcs' t1 #d1 t2 #d2 new_post) in
      d.export eff_dcs' f'
    )
}

instance exportable_arrow_pre_post
  (t1:Type) (t2:Type)
  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ arg_typ (root dcs) == unit /\ (ret_typ (root dcs) == unit)})
  {| d1:importable t1 fl pi (left dcs) |}
  {| d2:exportable t2 fl pi (right dcs) |}
  (pre : trace -> Type0)
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h. (check (root dcs)) () h () [] ==> pre h))
  (#c1 : squash (forall h lt r. pre h /\ post h r lt ==> enforced_locally pi h lt)) :
  exportable (t1 -> MIO (resexn t2) fl pre post) fl pi dcs = {
    ityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi; 
    c_ityp = solve;
    export = (fun eff_dcs (f:(t1 -> MIO (resexn t2) fl pre post)) ->
      let (| (| a, b, dc |), eff_dc |) = root eff_dcs in
      let eff_dc : eff_dc_typ fl #unit #unit dc = rityp_eff_dc eff_dc in
      let f' = enforce_pre pre dc eff_dc post f in
      let dc_pre = (fun x h -> dc () h () []) in
      let new_post = trivialize_new_post dc_pre (fun _ -> post) in
      let dcs' = (EmptyNode (left dcs) (right dcs)) in
      let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
      let d = (exportable_arrow_post_args #fl #pi #dcs' t1 #d1 t2 #d2 new_post) in
      d.export eff_dcs' f'
    )
}

    
(** *** Safe importable instances **)
let interm_is_safely_importable (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){Leaf? dcs}) #t (d:interm t fl pi) : safe_importable t fl pi dcs = {
  sityp = t;
  c_sityp = solve;
  safe_import = (fun x Leaf -> x); 
}

instance importable_unit (#fl:erased tflag) (#pi:policy_spec) : importable unit fl pi Leaf = {
  ityp = unit;
  c_ityp = solve;
  import = (fun () Leaf -> Inl ())
}

instance importable_file_descr (#fl:erased tflag) (#pi:policy_spec) : importable file_descr fl pi Leaf = {
  ityp = file_descr;
  c_ityp = solve;
  import = (fun fd Leaf -> Inl fd)
}

instance importable_bytes (#fl:erased tflag) (#pi:policy_spec) : importable Bytes.bytes fl pi Leaf = {
  ityp = Bytes.bytes;
  c_ityp = solve;
  import = (fun b Leaf -> Inl b)
}

(** *** Importable instances **)

instance safe_importable_is_importable (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc) #t (d:safe_importable t fl pi dcs) : importable t fl pi dcs = {
  ityp = d.sityp;
  c_ityp = solve;
  import = (fun x eff_dcs -> Inl (d.safe_import x eff_dcs))
}

instance importable_refinement
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc)
  t {| d:importable t fl pi dcs |}
  (rp : t -> Type0) {| d1:checkable rp |} :
  Tot (importable (x:t{rp x}) fl pi dcs) = {
  ityp = d.ityp;
  c_ityp = solve;
  import = (fun (x:d.ityp) eff_dcs ->
    (match d.import x eff_dcs with
    | Inl x ->
      if d1.check x then Inl x 
      else Inr Contract_failure
    | Inr err -> Inr err) <: resexn (x:t{rp x}))
}
    
instance importable_option
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc)
  t {| d:importable t fl pi dcs |} :
  Tot (importable (option t) fl pi dcs) = {
  ityp = option d.ityp;
  c_ityp = solve;
  import = (fun (x:option d.ityp) eff_dcs ->
    match x with
    | Some x' -> begin
      match d.import x' eff_dcs with
      | Inl x'' -> Inl (Some x'')
      | Inr err -> Inr err
    end
    | None -> Inr Contract_failure)
}

instance importable_pair
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 t2 {| d1:importable t1 fl pi (left dcs) |} {| d2:importable t2 fl pi (right dcs) |} :
  Tot (importable (t1 * t2) fl pi dcs) = {
  ityp = d1.ityp * d2.ityp;
  c_ityp = solve;
  import = (fun (x,y) eff_dcs ->
      match (d1.import x (left eff_dcs), d2.import y (right eff_dcs)) with
      | (Inl x, Inl y) -> Inl (x, y)
      | _ -> Inr Contract_failure)
}

instance importable_either
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 t2 {| d1:importable t1 fl pi (left dcs) |} {| d2:importable t2 fl pi (right dcs) |} :
  Tot (importable (either t1 t2) fl pi dcs) = {
  ityp = either d1.ityp d2.ityp;
  c_ityp = solve;
  import = (fun x eff_dcs ->
      match x with
      | Inl x' -> begin
        match d1.import x' (left eff_dcs) with
        | Inl x'' -> Inl (Inl x'')
        | Inr err -> Inr err
      end
      | Inr y -> begin
        match d2.import y (right eff_dcs) with
        | Inl y' -> Inl (Inr y')
        | Inr err -> Inr err
      end)
}

instance importable_dpair_refined
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  t1 t2 (p:t1 -> t2 -> Type0)
  {| d1:importable t1 fl pi (left dcs) |} {| d2:importable t2 fl pi (right dcs) |}
  {| d3:checkable2 p |} :
  Tot (importable (x:t1 & y:t2{p x y}) fl pi dcs) = {
  ityp = d1.ityp & d2.ityp;
  c_ityp = solve;
  import = (fun ((x', y')) eff_dcs ->
      match (d1.import x' (left eff_dcs), d2.import y' (right eff_dcs)) with
       | (Inl x, Inl y) ->
            if check2 #t1 #t2 #p x y then Inl ((| x, y |) <: (x:t1 & y:t2{p x y})) else Inr Contract_failure
       | _ -> Inr Contract_failure)
}

(** *** Safe importable arrows **)
instance safe_importable_resexn
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:tree pck_dc)
  t1 {| d1:importable t1 fl pi dcs |} :
  Tot (safe_importable (resexn t1) fl pi dcs) = {
  sityp = resexn d1.ityp;
  c_sityp = solve;
  safe_import = (fun x eff_dcs ->
      match x with
      | Inl x' -> d1.import x' eff_dcs 
      | Inr y -> Inr y)
}
    
instance safe_importable_arrow
  (#fl:erased tflag) (#pi:policy_spec) (#dcs:(tree pck_dc){EmptyNode? dcs})
  (t1:Type) {| d1:exportable t1 fl pi (left dcs) |}
  (t2:Type) {| d2:importable t2 fl pi (right dcs) |} : 
  safe_importable ((x:t1) -> MIOpi (resexn t2) fl pi) fl pi dcs = {
  sityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
  c_sityp = solve;
  safe_import = (fun (f:d1.ityp -> MIOpi (resexn d2.ityp) fl pi) eff_dcs (x:t1) -> 
    (let x' = d1.export (left eff_dcs) x in 
     let y : resexn d2.ityp = f x' in
     (safe_importable_resexn t2 #d2).safe_import y (right eff_dcs)) <: MIOpi (resexn t2) fl pi)
}

(** The following four should be unified but I had universe problems **)
let enforce_post_args_res
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (dc : dc_typ t1 (resexn t2))
  (eff_dc : eff_dc_typ fl dc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (dc x h r lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) =
  let (| h, eff_dc' |) = eff_dc x in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_dc' r then r
  else Inr Contract_failure

let enforce_post_args
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (dc : dc_typ t1 unit)
  (eff_dc : eff_dc_typ fl dc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (dc x h () lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) =
  let (| h, eff_dc' |) = eff_dc x in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_dc' () then r
  else Inr Contract_failure
  
let enforce_post_res
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (dc : dc_typ unit (resexn t2))
  (eff_dc : eff_dc_typ fl dc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (dc () h r lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) =
  let (| h, eff_dc' |) = eff_dc () in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_dc' r then r
  else Inr Contract_failure

let enforce_post
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (dc : dc_typ unit unit)
  (eff_dc : eff_dc_typ fl dc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (dc () h () lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) =
  let (| h, eff_dc' |) = eff_dc () in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_dc' () then r
  else Inr Contract_failure

instance safe_importable_arrow_pre_post_args_res
  (#t1:Type) (#t2:Type)
  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ (arg_typ (root dcs) == t1 /\ (ret_typ (root dcs) == (resexn t2))) })
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post: squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ check (root dcs) x h r lt ==> post x h r lt)) 
  {| d1:exportable t1 fl pi (left dcs) |}
  {| d2:importable t2 fl pi (right dcs) |}:
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) fl pi dcs = {
   sityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
  c_sityp = solve;
  safe_import = (fun (f:(d1.ityp -> MIOpi (resexn d2.ityp) fl pi)) eff_dcs ->
    let dcs' = (EmptyNode (left dcs) (right dcs)) in
    let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
    let f' = (safe_importable_arrow #fl #pi #dcs' t1 #d1 t2 #d2).safe_import f eff_dcs' in
    let (| dc_pck, eff_dc |) = root eff_dcs in
    enforce_post_args_res pi pre post (Mkdtuple3?._3 dc_pck) (rityp_eff_dc eff_dc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post_res
  (#t1:Type) (#t2:Type)
  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ (arg_typ (root dcs) == unit /\ (ret_typ (root dcs) == (resexn t2))) })
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post: squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ ((Mkdtuple3?._3 (root dcs)) () h r lt) ==> post x h r lt)) 
  {| d1:exportable t1 fl pi (left dcs) |}
  {| d2:importable t2 fl pi (right dcs) |}:
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) fl pi dcs = {
   sityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
  c_sityp = solve;
  safe_import = (fun (f:(d1.ityp -> MIOpi (resexn d2.ityp) fl pi)) eff_dcs ->
    let dcs' = (EmptyNode (left dcs) (right dcs)) in
    let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
    let f' = (safe_importable_arrow #fl #pi #dcs' t1 #d1 t2 #d2).safe_import f eff_dcs' in
    let (| dc_pck, eff_dc |) = root eff_dcs in
    enforce_post_res pi pre post (Mkdtuple3?._3 dc_pck) (rityp_eff_dc eff_dc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post_args
  (#t1:Type) (#t2:Type)
  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ (arg_typ (root dcs) == t1 /\ (ret_typ (root dcs) == unit)) })
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ ((Mkdtuple3?._3 (root dcs)) x h () lt) ==> post x h r lt))
  {| d1:exportable t1 fl pi (left dcs) |}
  {| d2:importable t2 fl pi (right dcs) |} :
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) fl pi dcs = {
    sityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
    c_sityp = solve;
    safe_import = (fun (f:(d1.ityp -> MIOpi (resexn d2.ityp) fl pi)) eff_dcs ->
      let dcs' = (EmptyNode (left dcs) (right dcs)) in
      let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
      let f' = (safe_importable_arrow #fl #pi #dcs' t1 #d1 t2 #d2).safe_import f eff_dcs' in
      let (| dc_pck, eff_dc |) = root eff_dcs in
      enforce_post_args pi pre post (Mkdtuple3?._3 dc_pck) (rityp_eff_dc eff_dc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post
  (#t1:Type) (#t2:Type)
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)

  (#fl:erased tflag)
  (#pi:policy_spec) 
  (#dcs:(tree pck_dc){Node? dcs /\ (arg_typ (root dcs) == unit /\ (ret_typ (root dcs) == unit)) })

  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ check (root dcs) () h () lt ==> post x h r lt)) 

  {| d1:exportable t1 fl pi (left dcs) |}
  {| d2:importable t2 fl pi (right dcs) |} :
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) fl pi dcs = {
    sityp = d1.ityp -> MIOpi (resexn d2.ityp) fl pi;
    c_sityp = solve;
    safe_import = (fun (f:(d1.ityp -> MIOpi (resexn d2.ityp) fl pi)) eff_dcs ->
      let dcs' = (EmptyNode (left dcs) (right dcs)) in
      let eff_dcs' = (EmptyNode (left eff_dcs) (right eff_dcs)) in
      let f' = (safe_importable_arrow #fl #pi #dcs' t1 #d1 t2 #d2).safe_import f eff_dcs' in
      let (| dc_pck, eff_dc |) = root eff_dcs in
      enforce_post pi pre post (Mkdtuple3?._3 dc_pck) (rityp_eff_dc eff_dc) c1post c2post f')
  }
