module Compiler.MIO.To.Weak

open FStar.Tactics
open FStar.Tactics.Typeclasses
open FStar.Ghost

open Compiler.Languages
open TC.Checkable


(** **** Types **)
(** Dynamic check possibilities:
pre:
  trace -> bool
  'a -> trace -> bool

post:
  trace -> trace -> bool
  'a -> trace -> trace -> bool
  trace -> 'b -> trace -> bool
  'a -> trace -> 'b -> trace -> bool

All these dynamic checks can be encoded using:
  'a -> trace -> 'b -> trace -> bool
and do some hacks in the code.

One extra thing would be the dynamic checks to be also
used to do the refinements. Right now, a different checkable
typeclass is used for the refinements.
**)

(** **** Tree **)
type tree (a: Type) =
  | Leaf : tree a
  | EmptyNode: left: tree a -> right: tree a -> tree a
  | Node: data: a -> left: tree a -> right: tree a -> tree a

let root (t:(tree 'a){Node? t}) = Node?.data t
(** TODO: refactor these into two utils **)
let left (t:(tree 'a){Node? t \/ EmptyNode? t}) : tree 'a = 
  match t with 
  | Node _ lt _ -> lt
  | EmptyNode lt _ -> lt

let right (t:(tree 'a){Node? t \/ EmptyNode? t}) : tree 'a = 
  match t with 
  | Node _ _ rt -> rt
  | EmptyNode _ rt -> rt
  
let rec equal_trees (t1:tree 'a) (t2:tree 'a) =
  match t1, t2 with
  | Leaf, Leaf -> True
  | EmptyNode lhs1 rhs1, EmptyNode lhs2 rhs2 -> equal_trees lhs1 lhs2 /\ equal_trees rhs1 rhs2
  | Node x lhs1 rhs1, Node y lhs2 rhs2 -> x == y /\ equal_trees lhs1 lhs2 /\ equal_trees rhs1 rhs2
  | _, _ -> False

(* The function above is really just propositional equality. See this proof. *)
let rec equal_trees_prop (t1 t2 : tree 'a) : Lemma (equal_trees t1 t2 <==> t1 == t2)
  [SMTPat (equal_trees t1 t2)]
  =
  match t1, t2 with
  | Leaf,            Leaf -> ()
  | EmptyNode l1 r1, EmptyNode l2 r2 
  | Node _ l1 r1,    Node _ l2 r2 -> equal_trees_prop l1 l2; equal_trees_prop r1 r2
  | _ -> ()

let rec map_tree (t:tree 'a) (f:'a -> 'b) : tree 'b =
  match t with
  | Leaf -> Leaf
  | EmptyNode lhs rhs -> EmptyNode (map_tree lhs f) (map_tree rhs f)
  | Node x lhs rhs -> Node (f x) (map_tree lhs f) (map_tree rhs f)
  
let rec map_fuse (t:tree 'a) (f:'a -> 'b) (g : 'b -> 'c)
  : Lemma (map_tree (map_tree t f) g == map_tree t (fun x -> g (f x)))
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_fuse lhs f g; map_fuse rhs f g
  | Node x lhs rhs -> map_fuse lhs f g; map_fuse rhs f g
  
let rec map_id (t:tree 'a)
  : Lemma (map_tree t (fun x -> x) == t)
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_id lhs; map_id rhs
  | Node x lhs rhs -> map_id lhs; map_id rhs
  
let rec map_ext (t:tree 'a) (f:'a -> 'b) (g : 'a -> 'b)
  : Lemma (requires (forall x. f x == g x))
          (ensures (map_tree t f == map_tree t g))
  =
  match t with
  | Leaf -> ()
  | EmptyNode lhs rhs -> map_ext lhs f g; map_ext rhs f g
  | Node x lhs rhs -> map_ext lhs f g; map_ext rhs f g

(* From h', an extension of h, we can obtain the difference
(i.e. the local trace) just from the lengths. *)
let get_local_trace (h':erased trace) (h:erased trace) :
  Pure (erased trace)
    (requires (h' `suffix_of` h))
    (ensures (fun lt -> h == hide (apply_changes h' lt))) =
  suffix_of_length h' h;
  let n : nat = (List.length h) - (List.length h') in
  let (lt', ht) = List.Tot.Base.splitAt n h in
  lemma_splitAt_equal n h;
  lemma_splitAt_suffix h h';
  List.Tot.Properties.rev_involutive lt';
  assert (reveal h == apply_changes h' (List.rev lt'));
  List.rev lt'

(* Idealized Runtime check typ *)
type irc_typ (argt:Type u#a) (rett:Type u#b) = argt -> trace -> rett -> trace -> Type0

(* Runtime check typ *)
type rc_typ_0 (mst:mst) (argt:Type u#a) (rett:Type u#b) = argt -> mst.cst -> rett -> mst.cst -> bool

type rc_typ (mst:mst) (#argt:Type u#a) (#rett:Type u#b) (irc : irc_typ argt rett)
  = rc:(rc_typ_0 mst argt rett)
         {forall x s0 h y s1 lt.
           mst.models s0 h /\ mst.models s1 (apply_changes h lt) ==> // if the states model the traces
             (rc x s0 y s1 <==> irc x h y lt)} // the runtime check coincides with idealized check
         
(* Postcondition for effectful runtime check *)
let eff_rc_typ_cont_post (mst:mst) #irc (rc:rc_typ mst irc) (initial_h:erased trace) (x:'a) (y:'b) (current_h:trace) (b:bool) (lt:trace) : Type0 =
  (initial_h `suffix_of` current_h) /\
  (let the_lt = get_local_trace initial_h current_h in
    apply_changes initial_h the_lt == current_h // the_lt = current_h - initial_h (implied by the spec of get_local_trace...)
    /\ lt == [] // no events generated by this check
    /\ (b <==> irc x initial_h y the_lt)) // the result is the same as the pure rc.

(* Effectful runtime check for the result, given initial value
and initial history *)
type eff_rc_typ_cont (mst:mst) (fl:erased tflag) (t1:Type u#a) (t2:Type u#b) (irc : irc_typ t1 t2) (rc:rc_typ mst irc) (x:t1) (initial_h:erased trace) =
  y:t2 -> MIO bool mst fl (fun h -> initial_h `suffix_of` h) (eff_rc_typ_cont_post mst rc initial_h x y)
  
(* Effectful runtime check: given an x:t1 returns an erased trace and an
effectful function to check the result (t2) *)
type eff_rc_typ (mst:mst) (fl:erased tflag) (#t1 #t2:Type) (irc:irc_typ t1 t2) (rc:rc_typ mst irc) =
  x:t1 -> MIO (s0:mst.cst & h0:erased trace{mst.models s0 h0} & eff_rc_typ_cont mst fl t1 t2 irc rc x h0)
             mst
             fl
             (fun _ -> True)
             (fun h (| s0, h0, _ |) lt -> h == reveal h0 /\ lt == [])

(* Lifting a runtime check into an effectful check *)
val enforce_rc : (#mst:mst) -> (#argt:Type u#a) -> (#rett:Type u#b) ->
  irc:irc_typ argt rett ->
  rc:rc_typ mst irc -> eff_rc_typ mst AllActions irc rc
#push-options "--compat_pre_core 1" // fixme
let enforce_rc #mst #argt #rett irc rc x =
  let h0 = get_trace true in
  let s0 = get_state true in
  assert (mst.models s0 h0);
  let cont : eff_rc_typ_cont mst AllActions argt rett irc rc x h0 =
    (fun (y:rett) ->
      let current_h = get_trace true in
      let s1 = get_state true in
      let lt = get_local_trace h0 current_h in
      rc x s0 y s1)
  in
  (| s0, h0 , cont |)
#pop-options

// todo: in HO cases, t1 or t2 should be unit since one can not write a
// runtime check that uses an MIO arrow. thus, one idea is to make the
// type of pck_rc here `pck_rc_typ (option t1) (option t2)`
type pck_rc mst = (argt:Type u#a & rett:Type u#b & irc:irc_typ argt rett & rc_typ mst irc)

let arg_typ #mst (ctr:pck_rc mst) : Type = ctr._1
let ret_typ #mst (ctr:pck_rc mst) : Type = ctr._2

let icheck #mst (ctr:pck_rc mst) (arg:arg_typ ctr) (h0:trace) (ret:ret_typ ctr) (h1:trace) : Type0 =
  ctr._3 arg h0 ret h1
  
let check #mst (ctr:pck_rc mst) (arg:arg_typ ctr) (s0:mst.cst) (ret:ret_typ ctr) (s1:mst.cst) : bool =
  ctr._4 arg s0 ret s1

let lemma_check_icheck (mst:mst) (ctr:pck_rc mst) (arg:arg_typ ctr) (ret:ret_typ ctr)
  (s0 s1 : mst.cst) (h0 : trace) (lt : list event)
  : Lemma (requires mst.models s0 h0 /\ mst.models s1 (apply_changes h0 lt))
          (ensures check ctr arg s0 ret s1 <==> icheck ctr arg h0 ret lt)
  = ()

type eff_pck_rc mst (fl:erased tflag) = ctr:pck_rc mst & eff_rc_typ mst fl ctr._3 ctr._4

val make_rc_eff : mst:mst -> pck_rc u#a u#b mst -> eff_pck_rc u#a u#b mst AllActions
let make_rc_eff mst r = (| r, (enforce_rc #mst r._3 r._4) |)

type typ_eff_rcs mst (fl:erased tflag) (rcs:tree (pck_rc mst)) =
  eff_rcs:(tree (eff_pck_rc mst fl)){equal_trees rcs (map_tree eff_rcs dfst)}

(* Two helpers, to guide F* *)
let typ_left (#mst:mst) (#fl:erased tflag) (#rcs:tree (pck_rc mst))
  ($t : typ_eff_rcs mst fl rcs{Node? t \/ EmptyNode? t})
  : typ_eff_rcs mst fl (left rcs)
  = left t

let typ_right (#mst:mst) (#fl:erased tflag) (#rcs:tree (pck_rc mst))
  ($t : typ_eff_rcs mst fl rcs{Node? t \/ EmptyNode? t})
  : typ_eff_rcs mst fl (right rcs)
  = right t
  
let cong (#t1 #t2 : tree 'a) (f : 'a -> 'b) (_ : squash (t1 == t2)) : squash (map_tree t1 f == map_tree t2 f) = ()

let make_rcs_eff #mst (rcs:tree (pck_rc mst)) : typ_eff_rcs mst AllActions rcs =
  let r : tree (eff_pck_rc mst AllActions) = map_tree rcs (make_rc_eff mst) in
  let r_def : squash (r == map_tree rcs (make_rc_eff mst)) = () in
  let comp x = dfst (make_rc_eff mst x) in
  (* sigh, F* not being very helpful here *)
  calc (==) {
    map_tree r dfst;
    == { cong dfst r_def }
    map_tree (map_tree #(pck_rc mst) rcs (make_rc_eff mst)) dfst;
    == { map_fuse #_ #(eff_pck_rc mst AllActions) #_ rcs (make_rc_eff mst) dfst }
    map_tree rcs comp;
    == { map_ext rcs (fun x -> dfst (make_rc_eff mst x)) (fun x -> x) }
    map_tree rcs (fun x -> x);
    == { map_id rcs }
    rcs;
  };
  r

class exportable (styp : Type u#a) (mst:mst) (pi:policy_spec) (rcs:tree (pck_rc u#c u#d mst)) (fl:erased tflag) = {
  [@@@no_method]
  wtyp : Type u#b;
  [@@@no_method]
  c_wtyp : weak wtyp mst fl pi;
  [@@@no_method]
  export : typ_eff_rcs mst fl rcs -> styp -> wtyp;
}

class safe_importable (styp : Type u#a) (mst:mst) (pi:policy_spec) (rcs:tree (pck_rc u#c u#d mst)) (fl:erased tflag) = {
  [@@@no_method]
  swtyp : Type u#b;
  [@@@no_method]
  c_swtyp : weak swtyp mst fl pi;
  [@@@no_method]
  safe_import : swtyp -> typ_eff_rcs mst fl rcs -> styp; 
}

class importable (styp : Type u#a) (mst:mst) (pi:policy_spec) (rcs:tree (pck_rc u#c u#d mst)) (fl:erased tflag) = {
  [@@@no_method]
  wtyp : Type u#b; 
  [@@@no_method]
  c_wtyp : weak wtyp mst fl pi;
  [@@@no_method]
  import : wtyp -> typ_eff_rcs mst fl rcs -> resexn styp;
}

instance weak_importable_super styp mst pi rcs fl (d : importable styp mst pi rcs fl) : weak d.wtyp mst fl pi = d.c_wtyp
instance weak_exportable_super styp mst pi rcs fl (d : exportable styp mst pi rcs fl) : weak d.wtyp mst fl pi = d.c_wtyp
instance weak_safe_importable_super styp mst pi rcs fl (d : safe_importable styp mst pi rcs fl) : weak d.swtyp mst fl pi = d.c_swtyp

(** *** Exportable instances **)

instance weak_is_exportable #mst (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){Leaf? rcs}) (#fl:erased tflag) t {| d1: weak t mst fl pi |} : exportable t mst pi rcs fl = {
  wtyp = t;
  c_wtyp = solve;
  export = (fun Leaf x -> x)
}

instance exportable_unit #mst (#pi:policy_spec) (#fl:erased tflag) : exportable unit mst pi Leaf fl = {
  wtyp = unit;
  c_wtyp = solve;
  export = (fun Leaf () -> ())
}

instance exportable_file_descr #mst (#pi:policy_spec) (#fl:erased tflag) : exportable file_descr mst pi Leaf fl = {
  wtyp = file_descr;
  c_wtyp = solve;
  export = (fun Leaf fd -> fd)
}

instance exportable_bytes #mst (#pi:policy_spec) (#fl:erased tflag) : exportable Bytes.bytes mst pi Leaf fl = {
  wtyp = Bytes.bytes;
  c_wtyp = solve;
  export = (fun Leaf b -> b)
}

instance exportable_refinement #mst (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag) t {| d:exportable t mst pi rcs fl |} (p : t -> Type0) : exportable (x:t{p x}) mst pi rcs fl = {
  wtyp = d.wtyp;
  c_wtyp = solve;
  export = d.export
}

instance exportable_option
  #mst
  (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag)
  t1 {| d1:exportable t1 mst pi rcs fl |} :
  Tot (exportable (option t1) mst pi rcs fl) = {
  wtyp = option d1.wtyp;
  c_wtyp = solve;
  export = (fun eff_rcs x -> match x with | Some x' -> Some (d1.export eff_rcs x') | None -> None)
}


instance exportable_pair
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 {| d1:exportable t1 mst pi (left rcs) fl |} t2 {| d2:exportable t2 mst pi (right rcs) fl |} :
  Tot (exportable (t1 * t2) mst pi rcs fl) = {
  wtyp = d1.wtyp * d2.wtyp;
  c_wtyp = solve;
  export = (fun eff_rcs (x, y) -> (d1.export (left eff_rcs) x, d2.export (right eff_rcs) y));
}

instance exportable_either
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 {| d1:exportable t1 mst pi (left rcs) fl |} t2 {| d2:exportable t2 mst pi (right rcs) fl |} :
  Tot (exportable (either t1 t2) mst pi rcs fl) = {
  wtyp = either d1.wtyp d2.wtyp;
  c_wtyp = solve;
  export = (fun eff_rcs x -> 
      match x with | Inl x -> Inl (d1.export (left eff_rcs) x) | Inr x -> Inr (d2.export (right eff_rcs) x))
}

(** *** Exportable arrows **)

#push-options "--compat_pre_core 1"
instance exportable_arrow_with_no_pre_and_no_post
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  (t1:Type) {| d1:importable t1 mst pi (left rcs) fl |}
  (t2:Type) {| d2:exportable t2 mst pi (right rcs) fl |} :
  exportable (t1 -> MIOpi (resexn t2) mst fl pi) mst pi rcs fl = {
    wtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi;
    c_wtyp = solve;
    export = (fun eff_rcs (f:(t1 -> MIOpi (resexn t2) mst fl pi)) (x:d1.wtyp) ->
      match d1.import x (left eff_rcs) with
      | Inl x' -> begin
        match f x' with 
        | Inl x'' -> Inl (d2.export (right eff_rcs) x'') 
        | Inr err -> Inr err
      end
      | Inr err -> Inr err
    )
  }
#pop-options

(** This is a design choice for making proofs easier. One can remove the post-condition **)
instance exportable_arrow_post_args
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 {| d1:importable t1 mst pi (left rcs) fl |}
  t2 {| d2:exportable t2 mst pi (right rcs) fl |}
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c1 : squash (forall x h lt r. post x h r lt ==> enforced_locally pi h lt)) :
  exportable (x:t1 -> MIO (resexn t2) mst fl (fun _ -> True) (post x)) mst pi rcs fl = {
    wtyp = x:d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi;
    c_wtyp = solve;
    export = (fun eff_rcs (f:(x:t1 -> MIO (resexn t2) mst fl (fun _ -> True) (post x))) ->
      let f' : t1 -> MIOpi (resexn t2) mst fl pi = f in
      (exportable_arrow_with_no_pre_and_no_post t1 #d1 t2 #d2).export eff_rcs f');
  }

instance exportable_arrow_post
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 {| d1:importable t1 mst pi (left rcs) fl |}
  t2 {| d2:exportable t2 mst pi (right rcs) fl |}
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c1 : squash (forall h lt r. post h r lt ==> enforced_locally pi h lt)) :
  exportable (t1 -> MIO (resexn t2) mst fl (fun _ -> True) post) mst pi rcs fl = 
  exportable_arrow_post_args t1 t2 (fun _ -> post)

let trivialize_new_post #a #b (pre: a -> trace -> Type0) post :
  Tot (a -> trace -> resexn b -> trace -> Type0) =
    fun x h r lt -> 
      (~(pre x h) ==> r == (Inr Contract_failure) /\ lt == []) /\
      (pre x h ==> post x h r lt) 

let enforce_pre
  #t1 #t2
  #mst
  (#fl:erased tflag)
  (pre : trace -> Type0)
  (irc : irc_typ unit unit)
  (rc : rc_typ mst irc)
  (eff_rc : eff_rc_typ mst fl irc rc) 
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h. irc () h () [] ==> pre h))
  (f:(t1 -> MIO (resexn t2) mst fl pre post))
  (x:t1) :
  MIO (resexn t2) mst fl (fun _ -> True) (trivialize_new_post (fun x h -> irc () h () []) (fun _ -> post) ()) =
  let (| s, h, eff_rc' |) : (s0:mst.cst & h0:(erased trace){mst.models s0 h0} & eff_rc_typ_cont mst fl unit unit irc rc () h0) = eff_rc () in
  if eff_rc' () then f x 
  else Inr Contract_failure

let enforce_pre_args
  #t1 #t2
  #mst
  (#fl:erased tflag)
  (pre : t1 -> trace -> Type0)
  (irc : irc_typ t1 unit)
  (rc : rc_typ mst irc)
  (eff_rc : eff_rc_typ mst fl irc rc)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h x. irc x h () [] ==> pre x h))
  (f:(x:t1 -> MIO (resexn t2) mst fl (pre x) (post x)))
  (x:t1) :
  MIO (resexn t2) mst fl (fun _ -> True) (trivialize_new_post (fun x h -> irc x h () []) post x) =
  let (| s, h, eff_rc' |) : (s0 : mst.cst & h0:(erased trace){mst.models s0 h0} & eff_rc_typ_cont mst fl t1 unit irc rc x h0) = eff_rc x in
  if eff_rc' () then f x 
  else Inr Contract_failure

let rwtyp_irc (#a #b #c #d:Type) (irc:irc_typ a b) : Pure (irc_typ c d) (requires (a == c /\ b == d)) (ensures (fun _ -> True)) = irc

val rwtyp_eff_rc : #mst:_ -> (#fl:erased tflag) ->
  (#a:Type u#a) -> (#b:Type u#b) -> (#c:Type{c == a}) -> (#d:Type{d == b}) ->
  (#irc:irc_typ a b) ->
  (#rc:rc_typ mst irc) ->
  (t : eff_rc_typ mst fl irc rc) ->
  (eff_rc_typ mst fl #c #d (rwtyp_irc irc) rc)

#push-options "--compat_pre_core 1"
let rwtyp_eff_rc #mst #fl #a #b #c #d #irc #rc eff_rc (x:c) = 
    let (| s0, h0, cont |) = eff_rc x in
    let cont' : eff_rc_typ_cont mst fl c d (rwtyp_irc irc) rc x h0 = (fun (y:d) -> cont y) in
    (| s0, h0, cont' |)
#pop-options

instance exportable_arrow_pre_post_args
  (t1:Type) (t2:Type)
  (mst:mst)
  (#pi:policy_spec) 
  (#rcs:(tree (pck_rc mst)){Node? rcs /\ arg_typ (root rcs) == t1 /\ (ret_typ (root rcs) == unit)})
  (#fl:erased tflag)
  {| d1:importable t1 mst pi (left rcs) fl |}
  {| d2:exportable t2 mst pi (right rcs) fl |}
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h x. icheck (root rcs) x h () [] ==> pre x h)) // idealized check here
  (#c1 : squash (forall x h lt r. pre x h /\ post x h r lt ==> enforced_locally pi h lt)) :
  exportable (x:t1 -> MIO (resexn t2) mst fl (pre x) (post x)) mst pi rcs fl = {
    wtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi; 
    c_wtyp = solve;
    export = (fun eff_rcs (f:(x:t1 -> MIO (resexn t2) mst fl (pre x) (post x))) ->
      let (| (| a, b, irc, rc |), eff_rc |) = root eff_rcs in
      let eff_rc : eff_rc_typ mst fl #t1 #unit irc rc = rwtyp_eff_rc eff_rc in
      let f' = enforce_pre_args pre irc rc eff_rc post f in
      let irc_pre = (fun x h -> irc x h () []) in
      let new_post = trivialize_new_post irc_pre post in
      let rcs' = (EmptyNode (left rcs) (right rcs)) in
      let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
      let d = (exportable_arrow_post_args #mst #pi #rcs' t1 #d1 t2 #d2 new_post) in
      d.export eff_rcs' f'
    )
}

instance exportable_arrow_pre_post
  (t1:Type) (t2:Type)
  (mst:mst)
  (#pi:policy_spec) 
  (#rcs:(tree (pck_rc mst)){Node? rcs /\ arg_typ (root rcs) == unit /\ (ret_typ (root rcs) == unit)})
  (#fl:erased tflag)
  {| d1:importable t1 mst pi (left rcs) fl |}
  {| d2:exportable t2 mst pi (right rcs) fl |}
  (pre : trace -> Type0)
  (post : trace -> resexn t2 -> trace -> Type0) 
  (#c_pre : squash (forall h. (icheck (root rcs)) () h () [] ==> pre h)) // idealized
  (#c1 : squash (forall h lt r. pre h /\ post h r lt ==> enforced_locally pi h lt)) :
  exportable (t1 -> MIO (resexn t2) mst fl pre post) mst pi rcs fl = {
    wtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi; 
    c_wtyp = solve;
    export = (fun eff_rcs (f:(t1 -> MIO (resexn t2) mst fl pre post)) ->
      let (| (| a, b, irc, rc |), eff_rc |) = root eff_rcs in
      let eff_rc : eff_rc_typ mst fl #unit #unit irc rc = rwtyp_eff_rc eff_rc in
      let f' = enforce_pre pre irc rc eff_rc post f in
      let irc_pre = (fun x h -> irc () h () []) in
      let new_post = trivialize_new_post irc_pre (fun _ -> post) in
      let rcs' = (EmptyNode (left rcs) (right rcs)) in
      let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
      let d = (exportable_arrow_post_args #mst #_ #rcs' t1 #d1 t2 #d2 new_post) in
      d.export eff_rcs' f'
    )
}

    
(** *** Safe importable instances **)
let weak_is_safely_importable (#mst:mst) (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){Leaf? rcs}) (#fl:erased tflag) #t (d:weak t mst fl pi) : safe_importable t mst pi rcs fl = {
  swtyp = t;
  c_swtyp = solve;
  safe_import = (fun x Leaf -> x); 
}

instance importable_unit (#mst:mst) (#pi:policy_spec) (#fl:erased tflag) : importable unit mst pi Leaf fl = {
  wtyp = unit;
  c_wtyp = solve;
  import = (fun () Leaf -> Inl ())
}

instance importable_file_descr (#mst:mst) (#pi:policy_spec) (#fl:erased tflag) : importable file_descr mst pi Leaf fl = {
  wtyp = file_descr;
  c_wtyp = solve;
  import = (fun fd Leaf -> Inl fd)
}

instance importable_bytes (#mst:mst) (#pi:policy_spec) (#fl:erased tflag) : importable Bytes.bytes mst pi Leaf fl = {
  wtyp = Bytes.bytes;
  c_wtyp = solve;
  import = (fun b Leaf -> Inl b)
}

(** *** Importable instances **)

instance safe_importable_is_importable (#mst:mst) (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag) #t (d:safe_importable t mst pi rcs fl) : importable t mst pi rcs fl = {
  wtyp = d.swtyp;
  c_wtyp = solve;
  import = (fun x eff_rcs -> Inl (d.safe_import x eff_rcs))
}

instance importable_refinement
  #mst
  (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag)
  t {| d:importable t mst pi rcs fl |}
  (rp : t -> Type0) {| d1:checkable rp |} :
  Tot (importable (x:t{rp x}) mst pi rcs fl) = {
  wtyp = d.wtyp;
  c_wtyp = solve;
  import = (fun (x:d.wtyp) eff_rcs ->
    (match d.import x eff_rcs with
    | Inl x ->
      if d1.check x then Inl x 
      else Inr Contract_failure
    | Inr err -> Inr err) <: resexn (x:t{rp x}))
}
    
instance importable_option
  #mst
  (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag)
  t {| d:importable t mst pi rcs fl |} :
  Tot (importable (option t) mst pi rcs fl) = {
  wtyp = option d.wtyp;
  c_wtyp = solve;
  import = (fun (x:option d.wtyp) eff_rcs ->
    match x with
    | Some x' -> begin
      match d.import x' eff_rcs with
      | Inl x'' -> Inl (Some x'')
      | Inr err -> Inr err
    end
    | None -> Inr Contract_failure)
}

instance importable_pair
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 t2 {| d1:importable t1 mst pi (left rcs) fl |} {| d2:importable t2 mst pi (right rcs) fl |} :
  Tot (importable (t1 * t2) mst pi rcs fl) = {
  wtyp = d1.wtyp * d2.wtyp;
  c_wtyp = solve;
  import = (fun (x,y) eff_rcs ->
      match (d1.import x (left eff_rcs), d2.import y (right eff_rcs)) with
      | (Inl x, Inl y) -> Inl (x, y)
      | _ -> Inr Contract_failure)
}

instance importable_either
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 t2 {| d1:importable t1 mst pi (left rcs) fl |} {| d2:importable t2 mst pi (right rcs) fl |} :
  Tot (importable (either t1 t2) mst pi rcs fl) = {
  wtyp = either d1.wtyp d2.wtyp;
  c_wtyp = solve;
  import = (fun x eff_rcs ->
      match x with
      | Inl x' -> begin
        match d1.import x' (left eff_rcs) with
        | Inl x'' -> Inl (Inl x'')
        | Inr err -> Inr err
      end
      | Inr y -> begin
        match d2.import y (right eff_rcs) with
        | Inl y' -> Inl (Inr y')
        | Inr err -> Inr err
      end)
}

instance importable_dpair_refined
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  t1 t2 (p:t1 -> t2 -> Type0)
  {| d1:importable t1 mst pi (left rcs) fl |} {| d2:importable t2 mst pi (right rcs) fl |}
  {| d3:checkable2 p |} :
  Tot (importable (x:t1 & y:t2{p x y}) mst pi rcs fl) = {
  wtyp = d1.wtyp & d2.wtyp;
  c_wtyp = solve;
  import = (fun ((x', y')) eff_rcs ->
      match (d1.import x' (left eff_rcs), d2.import y' (right eff_rcs)) with
       | (Inl x, Inl y) ->
            if check2 #t1 #t2 #p x y then Inl ((| x, y |) <: (x:t1 & y:t2{p x y})) else Inr Contract_failure
       | _ -> Inr Contract_failure)
}

(** *** Safe importable arrows **)
instance safe_importable_resexn
  #mst
  (#pi:policy_spec) (#rcs:tree (pck_rc mst)) (#fl:erased tflag)
  t1 {| d1:importable t1 mst pi rcs fl |} :
  Tot (safe_importable (resexn t1) mst pi rcs fl) = {
  swtyp = resexn d1.wtyp;
  c_swtyp = solve;
  safe_import = (fun x eff_rcs ->
      match x with
      | Inl x' -> d1.import x' eff_rcs 
      | Inr y -> Inr y)
}

#push-options "--compat_pre_core 1"
instance safe_importable_arrow
  #mst
  (#pi:policy_spec) (#rcs:(tree (pck_rc mst)){EmptyNode? rcs}) (#fl:erased tflag)
  (t1:Type) {| d1:exportable t1 mst pi (left rcs) fl |}
  (t2:Type) {| d2:importable t2 mst pi (right rcs) fl |} : 
  safe_importable ((x:t1) -> MIOpi (resexn t2) mst fl pi) mst pi rcs fl = {
  swtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi;
  c_swtyp = solve;
  safe_import = (fun (f:d1.wtyp -> MIOpi (resexn d2.wtyp) mst fl pi) eff_rcs (x:t1) -> 
    (let x' = d1.export (left eff_rcs) x in 
     let y : resexn d2.wtyp = f x' in
     (safe_importable_resexn t2 #d2).safe_import y (right eff_rcs)) <: MIOpi (resexn t2) mst fl pi)
}
#pop-options

(** The following four should be unified but I had universe problems **)
let enforce_post_args_res
  (#t1:Type u#a)
  (#t2:Type u#b)
  #mst
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (irc : irc_typ t1 (resexn t2))
  (rc : rc_typ mst irc)
  (eff_rc : eff_rc_typ mst fl irc rc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (irc x h r lt) ==> post x h r lt)) //idealized checj
  (f:t1 -> MIOpi (resexn t2) mst fl pi)
  (x:t1) :
  MIO (resexn t2) mst fl (pre x) (post x) by (explode ()) =
  let (| s0, h0, eff_rc' |) = eff_rc x in
  Classical.forall_intro (lemma_suffixOf_append h0);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h0));
  if eff_rc' r then r
  else Inr Contract_failure

let enforce_post_args
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (rc : rc_typ t1 unit)
  (eff_rc : eff_rc_typ fl rc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (rc x h () lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) by (explode ()) =
  let (| h, eff_rc' |) = eff_rc x in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_rc' () then r
  else Inr Contract_failure
  
let enforce_post_res
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (rc : rc_typ unit (resexn t2))
  (eff_rc : eff_rc_typ fl rc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (rc () h r lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) by (explode ())=
  let (| h, eff_rc' |) = eff_rc () in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_rc' r then r
  else Inr Contract_failure

let enforce_post
  (#t1:Type u#a)
  (#t2:Type u#b)
  (#fl:erased tflag)
  (pi:policy_spec)
  (pre:t1 -> trace -> Type0)
  (post:t1 -> trace -> resexn t2 -> trace -> Type0)
  (rc : rc_typ unit unit)
  (eff_rc : eff_rc_typ fl rc) 
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> (post x h (Inr Contract_failure) lt)))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ (rc () h () lt) ==> post x h r lt))
  (f:t1 -> MIOpi (resexn t2) fl pi)
  (x:t1) :
  MIO (resexn t2) fl (pre x) (post x) by (explode ()) =
  let (| h, eff_rc' |) = eff_rc () in
  Classical.forall_intro (lemma_suffixOf_append h);
  let r : resexn t2 = f x in
  Classical.forall_intro_2 (Classical.move_requires_2 (lemma_append_rev_inv_tail h));
  if eff_rc' () then r
  else Inr Contract_failure

instance safe_importable_arrow_pre_post_args_res
  (#t1:Type) (#t2:Type)
  (#pi:policy_spec) 
  (#rcs:(tree pck_rc){Node? rcs /\ (arg_typ (root rcs) == t1 /\ (ret_typ (root rcs) == (resexn t2))) })
  (#fl:erased tflag)
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post: squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ check (root rcs) x h r lt ==> post x h r lt)) 
  {| d1:exportable t1 pi (left rcs) fl |}
  {| d2:importable t2 pi (right rcs) fl |}:
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) pi rcs fl = {
   swtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi;
  c_swtyp = solve;
  safe_import = (fun (f:(d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi)) eff_rcs ->
    let rcs' = (EmptyNode (left rcs) (right rcs)) in
    let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
    let f' = (safe_importable_arrow #_ #rcs' t1 #d1 t2 #d2).safe_import f eff_rcs' in
    let (| rc_pck, eff_rc |) = root eff_rcs in
    enforce_post_args_res pi pre post (Mkdtuple3?._3 rc_pck) (rwtyp_eff_rc eff_rc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post_res
  (#t1:Type) (#t2:Type)
  (#pi:policy_spec) 
  (#rcs:(tree pck_rc){Node? rcs /\ (arg_typ (root rcs) == unit /\ (ret_typ (root rcs) == (resexn t2))) })
  (#fl:erased tflag)
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post: squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ ((Mkdtuple3?._3 (root rcs)) () h r lt) ==> post x h r lt)) 
  {| d1:exportable t1 pi (left rcs) fl |}
  {| d2:importable t2 pi (right rcs) fl |}:
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) pi rcs fl = {
   swtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi;
  c_swtyp = solve;
  safe_import = (fun (f:(d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi)) eff_rcs ->
    let rcs' = (EmptyNode (left rcs) (right rcs)) in
    let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
    let f' = (safe_importable_arrow #_ #rcs' t1 #d1 t2 #d2).safe_import f eff_rcs' in
    let (| rc_pck, eff_rc |) = root eff_rcs in
    enforce_post_res pi pre post (Mkdtuple3?._3 rc_pck) (rwtyp_eff_rc eff_rc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post_args
  (#t1:Type) (#t2:Type)
  (#pi:policy_spec) 
  (#rcs:(tree pck_rc){Node? rcs /\ (arg_typ (root rcs) == t1 /\ (ret_typ (root rcs) == unit)) })
  (#fl:erased tflag)
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)
  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ ((Mkdtuple3?._3 (root rcs)) x h () lt) ==> post x h r lt))
  {| d1:exportable t1 pi (left rcs) fl |}
  {| d2:importable t2 pi (right rcs) fl |} :
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) pi rcs fl = {
    swtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi;
    c_swtyp = solve;
    safe_import = (fun (f:(d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi)) eff_rcs ->
      let rcs' = (EmptyNode (left rcs) (right rcs)) in
      let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
      let f' = (safe_importable_arrow #_ #rcs' t1 #d1 t2 #d2).safe_import f eff_rcs' in
      let (| rc_pck, eff_rc |) = root eff_rcs in
      enforce_post_args pi pre post (Mkdtuple3?._3 rc_pck) (rwtyp_eff_rc eff_rc) c1post c2post f')
  }

instance safe_importable_arrow_pre_post
  (#t1:Type) (#t2:Type)
  (pre : t1 -> trace -> Type0)
  (post : t1 -> trace -> resexn t2 -> trace -> Type0)

  (#pi:policy_spec) 
  (#rcs:(tree pck_rc){Node? rcs /\ (arg_typ (root rcs) == unit /\ (ret_typ (root rcs) == unit)) })

  (c1post : squash (forall x h lt. pre x h /\ enforced_locally pi h lt ==> post x h (Inr Contract_failure) lt))
  (c2post : squash (forall x h r lt. pre x h /\ enforced_locally pi h lt /\ check (root rcs) () h () lt ==> post x h r lt)) 

  (#fl:erased tflag)

  {| d1:exportable t1 pi (left rcs) fl |}
  {| d2:importable t2 pi (right rcs) fl |} :
  safe_importable (x:t1 -> MIO (resexn t2) fl (pre x) (post x)) pi rcs fl = {
    swtyp = d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi;
    c_swtyp = solve;
    safe_import = (fun (f:(d1.wtyp -> MIOpi (resexn d2.wtyp) fl pi)) eff_rcs ->
      let rcs' = (EmptyNode (left rcs) (right rcs)) in
      let eff_rcs' = (EmptyNode (left eff_rcs) (right eff_rcs)) in
      let f' = (safe_importable_arrow #_ #rcs' t1 #d1 t2 #d2).safe_import f eff_rcs' in
      let (| rc_pck, eff_rc |) = root eff_rcs in
      enforce_post pi pre post (Mkdtuple3?._3 rc_pck) (rwtyp_eff_rc eff_rc) c1post c2post f')
  }
